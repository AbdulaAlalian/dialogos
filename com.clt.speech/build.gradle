apply plugin: 'java'

repositories {
  flatDir {
    dirs '../libs'
  }
  mavenCentral()
}

task 'parser' {
  doLast {
    ant.taskdef classname: "java_cup.anttask.CUPTask", name: "cup", classpath: configurations.javaCup.asPath
    ant.cup srcfile: "${projectDir}/htk.cup", destdir: "${projectDir}/src/main/java/", parser: 'Parser', symbols: 'Sym'
  }
}

task 'lexer' {
  doLast {
    ant.taskdef classname: 'jflex.anttask.JFlexTask', name: 'flex', classpath: configurations.flex.asPath
    ant.flex file: "${projectDir}/htk.lex", destdir: "${projectDir}/src/main/java/"
  }
}

configurations {
    javaCup
    flex
}

compileJava.dependsOn parser
compileJava.dependsOn lexer

clean.doFirst {
  delete "${projectDir}/src/main/java/com/clt/speech/htk/Parser.java"
  delete "${projectDir}/src/main/java/com/clt/speech/htk/Lexer.java"
  delete "${projectDir}/src/main/java/com/clt/speech/htk/Sym.java"
}

dependencies {
  javaCup group: 'nz.ac.waikato.cms.weka.thirdparty', name: 'java-cup-11b', version: '2015.03.26'
  flex group: 'de.jflex', name: 'jflex', version: '1.7.0'

  implementation group: 'nz.ac.waikato.cms.weka.thirdparty', name: 'java-cup-11b', version: '2015.03.26'
  implementation group: 'nz.ac.waikato.cms.weka.thirdparty', name: 'java-cup-11b-runtime', version: '2015.03.26'
  
  implementation project(':com.clt.base')
  implementation project(':com.clt.script')
}
